#include <PID_v1.h>

// L298N H-Bridge Connection PINs (enA and enB are permanently high)
// Using PWM-capable pins: 3, 5, 6, 9, 10, 11
#define L298N_in1 6   // PWM Dir Motor A
#define L298N_in2 9   // PWM Dir Motor A
#define L298N_in3 10  // PWM Dir Motor B  
#define L298N_in4 11  // PWM Dir Motor B

// Wheel Encoders Connection PINs
#define right_encoder_phaseA 3  // Interrupt 
#define right_encoder_phaseB 5  
#define left_encoder_phaseA 2   // Interrupt
#define left_encoder_phaseB 4

// Encoders
unsigned int right_encoder_counter = 0;
unsigned int left_encoder_counter = 0;
String right_wheel_sign = "p";  // 'p' = positive, 'n' = negative
String left_wheel_sign = "p";   // 'p' = positive, 'n' = negative
unsigned long last_millis = 0;
const unsigned long interval = 100;

// Motor direction tracking
bool is_right_wheel_forward = true;
bool is_left_wheel_forward = true;

// Interpret Serial Messages
bool is_right_wheel_cmd = false;
bool is_left_wheel_cmd = false;
char value[] = "00.00";
uint8_t value_idx = 0;
bool is_cmd_complete = false;

// PID
// Setpoint - Desired
double right_wheel_cmd_vel = 0.0;     // rad/s
double left_wheel_cmd_vel = 0.0;      // rad/s
// Input - Measurement
double right_wheel_meas_vel = 0.0;    // rad/s
double left_wheel_meas_vel = 0.0;     // rad/s
// Output - Command
double right_wheel_cmd = 0.0;         // 0-255
double left_wheel_cmd = 0.0;          // 0-255
// Tuning
double Kp_r = 11.5;
double Ki_r = 7.5;
double Kd_r = 0.1;
double Kp_l = 12.8;
double Ki_l = 8.3;
double Kd_l = 0.1;
// Controller
PID rightMotor(&right_wheel_meas_vel, &right_wheel_cmd, &right_wheel_cmd_vel, Kp_r, Ki_r, Kd_r, DIRECT);
PID leftMotor(&left_wheel_meas_vel, &left_wheel_cmd, &left_wheel_cmd_vel, Kp_l, Ki_l, Kd_l, DIRECT);

void setup() {
  // Init L298N H-Bridge Connection PINs (all as PWM outputs)
  pinMode(L298N_in1, OUTPUT);
  pinMode(L298N_in2, OUTPUT);
  pinMode(L298N_in3, OUTPUT);
  pinMode(L298N_in4, OUTPUT);

  // Initialize motors in stopped state
  stopRightMotor();
  stopLeftMotor();

  rightMotor.SetMode(AUTOMATIC);
  leftMotor.SetMode(AUTOMATIC);
  Serial.begin(115200);

  // Init encoders
  pinMode(right_encoder_phaseB, INPUT);
  pinMode(left_encoder_phaseB, INPUT);
  // Set Callback for Wheel Encoders Pulse
  attachInterrupt(digitalPinToInterrupt(right_encoder_phaseA), rightEncoderCallback, RISING);
  attachInterrupt(digitalPinToInterrupt(left_encoder_phaseA), leftEncoderCallback, RISING);
}

void loop() {
  // Read and Interpret Wheel Velocity Commands
  if (Serial.available()) {
    char chr = Serial.read();
    // Right Wheel Motor
    if(chr == 'r') {
      is_right_wheel_cmd = true;
      is_left_wheel_cmd = false;
      value_idx = 0;
      is_cmd_complete = false;
    }
    // Left Wheel Motor
    else if(chr == 'l') {
      is_right_wheel_cmd = false;
      is_left_wheel_cmd = true;
      value_idx = 0;
    }
    // Positive direction
    else if(chr == 'p') {
      if(is_right_wheel_cmd) {
        is_right_wheel_forward = true;
      }
      else if(is_left_wheel_cmd) {
        is_left_wheel_forward = true;
      }
    }
    // Negative direction
    else if(chr == 'n') {
      if(is_right_wheel_cmd) {
        is_right_wheel_forward = false;
      }
      else if(is_left_wheel_cmd) {
        is_left_wheel_forward = false;
      }
    }
    // Separator
    else if(chr == ',') {
      if(is_right_wheel_cmd) {
        right_wheel_cmd_vel = atof(value);
      }
      else if(is_left_wheel_cmd) {
        left_wheel_cmd_vel = atof(value);
        is_cmd_complete = true;
      }
      // Reset for next command
      value_idx = 0;
      value[0] = '0';
      value[1] = '0';
      value[2] = '.';
      value[3] = '0';
      value[4] = '0';
      value[5] = '\0';
    }
    // Command Value
    else {
      if(value_idx < 5) {
        value[value_idx] = chr;
        value_idx++;
      }
    }
  }

  // Encoder and Motor Control
  unsigned long current_millis = millis();
  if(current_millis - last_millis >= interval) {
    // Calculate velocity: (pulses/interval) * (10 intervals/sec) * (1 rev/1125 pulses) * (2Ï€ rad/rev)
    // 1125 = 30 PPR * 37.5 gear ratio
    right_wheel_meas_vel = (10.0 * right_encoder_counter) * (2.0 * PI / 1125.0);
    left_wheel_meas_vel = (10.0 * left_encoder_counter) * (2.0 * PI / 1125.0);
    
    rightMotor.Compute();
    leftMotor.Compute();

    // Apply motor commands with direction control
    controlRightMotor();
    controlLeftMotor();

    String encoder_read = "r" + right_wheel_sign + String(right_wheel_meas_vel) + ",l" + left_wheel_sign + String(left_wheel_meas_vel) + ",";
    Serial.println(encoder_read);
    
    last_millis = current_millis;
    right_encoder_counter = 0;
    left_encoder_counter = 0;
  }
}

// Control Right Motor (Motor A)
void controlRightMotor() {
  if(right_wheel_cmd_vel == 0.0) {
    stopRightMotor();
  }
  else if(is_right_wheel_forward) {
    // Forward: in1 = PWM, in2 = LOW
    analogWrite(L298N_in1, (int)right_wheel_cmd);
    analogWrite(L298N_in2, 0);
  }
  else {
    // Reverse: in1 = LOW, in2 = PWM
    analogWrite(L298N_in1, 0);
    analogWrite(L298N_in2, (int)right_wheel_cmd);
  }
}

// Control Left Motor (Motor B)
void controlLeftMotor() {
  if(left_wheel_cmd_vel == 0.0) {
    stopLeftMotor();
  }
  else if(is_left_wheel_forward) {
    // Forward: in3 = PWM, in4 = LOW
    analogWrite(L298N_in3, (int)left_wheel_cmd);
    analogWrite(L298N_in4, 0);
  }
  else {
    // Reverse: in3 = LOW, in4 = PWM
    analogWrite(L298N_in3, 0);
    analogWrite(L298N_in4, (int)left_wheel_cmd);
  }
}

// Stop Right Motor
void stopRightMotor() {
  analogWrite(L298N_in1, 0);
  analogWrite(L298N_in2, 0);
}

// Stop Left Motor
void stopLeftMotor() {
  analogWrite(L298N_in3, 0);
  analogWrite(L298N_in4, 0);
}

// New pulse from Right Wheel Encoder
void rightEncoderCallback() {
  if(digitalRead(right_encoder_phaseB) == HIGH) {
    right_wheel_sign = "p";
  }
  else {
    right_wheel_sign = "n";
  }
  right_encoder_counter++;
}

// New pulse from Left Wheel Encoder
void leftEncoderCallback() {
  if(digitalRead(left_encoder_phaseB) == HIGH) {
    left_wheel_sign = "n";
  }
  else {
    left_wheel_sign = "p";
  }
  left_encoder_counter++;
}
